string getFileCount(java.io.File file) {
    var fs = file.listFiles()
    var tot = 0
    var javaNum = 0
    var hantNum = 0
    for (var i = 0; i < fs.length; i += 1) {
        var f = fs[i]
        if (f.isDirectory()) {
            var nextRes = getFileCount(f)
            var arr = nextRes.split(",")
            tot += java.lang.Integer.parseInt(arr[0])
            javaNum += java.lang.Integer.parseInt(arr[1])
            hantNum += java.lang.Integer.parseInt(arr[2])
        }
        if (f.isFile()) {
            tot += 1
            if (f.getPath().endsWith(".java")) javaNum += 1
            if (f.getPath().endsWith(".hant")) hantNum += 1
        }
    }
    return tot + "," + javaNum + "," + hantNum
}

string getLineCountMethod1(java.io.File file) {
    var fs = file.listFiles()
    var javaNum = 0
    var hantNum = 0
    var testJavaNum = 0
    for (var i = 0; i < fs.length; i += 1) {
        var f = fs[i]
        if (f.isDirectory()) {
            var nextRes = getLineCountMethod1(f)
            var arr = nextRes.split(",")
            javaNum += java.lang.Integer.parseInt(arr[0])
            hantNum += java.lang.Integer.parseInt(arr[1])
            testJavaNum += java.lang.Integer.parseInt(arr[2])
        }
        if (f.isFile()) {
            var fName = f.getPath()
            if fName.startsWith(".\\.git") {
                continue
            }
            var inp = new java.io.FileInputStream(fName)
            var bytes = inp.readAllBytes()
            var data = new java.lang.String(bytes)
            // var lineCount = data.split("\n").length // TODO: 目前不支持这种写法
            var lines = data.split("\n")
            var lineCount = lines.length
            if (fName.endsWith(".java")) javaNum += lineCount
            if (fName.endsWith(".hant")) hantNum += lineCount
            if fName.startsWith(".\\src\\test") testJavaNum += lineCount
        }
    }
    return `${javaNum},${hantNum},${testJavaNum},${javaNum - testJavaNum}`
}

string getLineCountMethod2(java.io.File file) {
    var fs = file.listFiles()
    var javaNum = 0
    var hantNum = 0
    for (var i = 0; i < fs.length; i += 1) {
        var f = fs[i]
        if (f.isDirectory()) {
            var nextRes = getLineCountMethod2(f)
            var arr = nextRes.split(",")
            javaNum += java.lang.Integer.parseInt(arr[0])
            hantNum += java.lang.Integer.parseInt(arr[1])
        }
        if (f.isFile()) {
            var fName = f.getPath()
            var inp = new java.io.FileInputStream(fName)
            var bytes = inp.readAllBytes()
            var data = new java.lang.String(bytes)
            var lineCount = 1
            for var i = 0; i < bytes.length; i += 1 {
                if bytes[i] == 10y {
                    lineCount += 1
                }
            }
            if (fName.endsWith(".java")) javaNum += lineCount
            if (fName.endsWith(".hant")) hantNum += lineCount
        }
    }
    return javaNum + "," + hantNum
}

_main {
    var path = "."
	var file = new java.io.File(path)
    var ans1 = getFileCount(file)
    print `${ans1} (total Java hant)` // 3832,192,55
    var ans2 = getLineCountMethod1(file)
    print `${ans2} (Java代码量 hant代码量 Java单测代码量 Java非单测代码量 accurate)` // 20147,3449,3207,16940
    var ans3 = getLineCountMethod2(file)
    print ans3 // 20330,3504 数据有差别，因为会有些大于1字节的字符的某个字节的值恰好是10
}

void main(string[] args) {
    var start = java.lang.System.currentTimeMillis();
    _main()
    var end = java.lang.System.currentTimeMillis()
    print `time cost: ${end - start} ms`
}
